///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, Щербаков Вадим, chtcherbakov.vadim@gmail.com
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ТолстыйКлиент = Ложь;
	#Если ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение  Тогда
	ТолстыйКлиент = Истина;
	#КонецЕсли

	Если НЕ ТолстыйКлиент Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Обработка должна быть запущена в режиме толстого клиента'"));
		Отказ = Истина;
	КонецЕсли;
	
	#Если ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение  Тогда
	ТабличныйДокумент1 = ПолучитьМакет("Макет1");
	ТабличныйДокумент2 = ПолучитьМакет("Макет2");
	УстановитьЗаголовокСистемы(ПолучитьМакет("Заголовок").ПолучитьТекст());
	
	ПодключитьОбработчикОжидания("ПослеОткрытия", 0.1, Истина);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПослеОткрытия()
	Сравнить();	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОбработкуСравнения(Команда)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.ПолноеИмяФайла = ПолучитьДопустимоеИмяФайла(ПолучитьМакет("Заголовок").ПолучитьТекст());
	ДиалогВыбораФайла.Фильтр = НСтр("ru = 'Внешняя обработка'") + "(*.epf)|*.epf";

	ОбработчикПродолжения = Новый ОписаниеОповещения("СохранитьОбработкуСравненияПослеВыбораФайла", ЭтаФорма);
	ДиалогВыбораФайла.Показать(ОбработчикПродолжения);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОбработкуСравненияПослеВыбораФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КопироватьФайл(ПолучитьИмяФайлаОбработки(), ВыбранныеФайлы[0]);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы <> Истина 
		И Модифицированность Тогда
		
		ОбработчикПродолжения = Новый ОписаниеОповещения("ПередЗакрытиемПослеВопроса", ЭтаФорма);
		ПоказатьВопрос(ОбработчикПродолжения, НСтр("ru = 'Измененные данные будут потеряны. Продолжить?'")
			,РежимДиалогаВопрос.ДаНет);
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемПослеВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы <> Истина Тогда
		ЗавершитьРаботуСистемы(Ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сравнить(Команда = Неопределено)
	#Если ТолстыйКлиентУправляемоеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
	ИмяВременногоФайла1 = ПолучитьИмяВременногоФайла("mxl");
	ТабличныйДокумент1.Записать(ИмяВременногоФайла1);
	
	ИмяВременногоФайла2 = ПолучитьИмяВременногоФайла("mxl");
	ТабличныйДокумент2.Записать(ИмяВременногоФайла2);
	
	СравнениеФайлов	= Новый СравнениеФайлов;
	СравнениеФайлов.СпособСравнения = СпособСравненияФайлов.ТабличныйДокумент;
	СравнениеФайлов.ПервыйФайл = ИмяВременногоФайла1;
	СравнениеФайлов.ВторойФайл = ИмяВременногоФайла2;
	
	СравнениеФайлов.ПоказатьРазличия();
	
	УдалитьФайлы(ИмяВременногоФайла1);
	УдалитьФайлы(ИмяВременногоФайла2);
	#КонецЕсли
КонецПроцедуры

&НаСервере
Функция ПолучитьМакет(ИмяМакета)
	Возврат РеквизитФормыВЗначение("Объект").ПолучитьМакет(ИмяМакета);
КонецФункции

&НаСервере
Функция ПолучитьИмяФайлаОбработки()
	Возврат РеквизитФормыВЗначение("Объект").ИспользуемоеИмяФайла;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьДопустимоеИмяФайла(Текст)
	Возврат СтрЗаменить(СтрЗаменить(Текст
		,"<", "_")
		,">", "_");
КонецФункции